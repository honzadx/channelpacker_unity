#pragma kernel CSMain

struct ChannelData
{
    int2 size;
    int2 offset;
    float2 clamp;
    float2 clip;
    int mask;
    int samplingType;
    int invert;
    float scaler;
    float defaultValue;
};

StructuredBuffer<ChannelData> channelDataBuffer : register(t0);
Texture2D inputR                                : register(t1);
Texture2D inputG                                : register(t2);
Texture2D inputB                                : register(t3);
Texture2D inputA                                : register(t4);

RWTexture2D<float4> result;

inline float ResolveChannel(uint index, uint2 id)
{   
    ChannelData channelData = channelDataBuffer[index];

    id -= channelData.offset;

    // ReSharper disable once CppDefaultCaseNotHandledInSwitchStatement
    switch(channelData.samplingType)
    {
        case 1:
            id = uint2(id.x % channelData.size.x, id.y % channelData.size.y);
            break;
    }
    
    float4 pixel;
    switch(index)
    {
        default:
        case 0:
            pixel = inputR[id.xy];
            break;
        case 1:
            pixel = inputG[id.xy];
            break;
        case 2:
            pixel = inputB[id.xy];
            break;
        case 3:
            pixel = inputA[id.xy];
            break;
    }

    int defaultMask = !(channelData.mask << 1) & 1;
    int textureMask = 1 - defaultMask;
    float result = defaultMask * channelData.defaultValue
    + (
        (channelData.mask >> 1 & 1) * pixel.r
        + (channelData.mask >> 2 & 1) * pixel.g
        + (channelData.mask >> 3 & 1) * pixel.b
        + (channelData.mask >> 4 & 1) * pixel.a
    ) * textureMask;

    if(textureMask)
    {
        result = lerp(result, 1 - result, channelData.invert);
        result = lerp(result, result * channelData.scaler, textureMask);

        if(result >= channelData.clip.y)
            result = 1;
        else if(result <= channelData.clip.x)
            result = 0;

        result = lerp(result, clamp(result, channelData.clamp.x, channelData.clamp.y), textureMask);
    }
    return result;
}

[numthreads(1,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    result[id.xy] = float4
    (
        ResolveChannel(0, id.xy),
        ResolveChannel(1, id.xy),
        ResolveChannel(2, id.xy),
        ResolveChannel(3, id.xy)
    );
}
